Command = require('../Command')

class Condition extends Command
  type: 'Condition'
  
  signature: [
  	if: ['Query', 'Selector', 'Variable', 'Constraint', 'Default'],
  	then: ['Any'], 
  	[
  		else: ['Any']
  	]
  ]

  cleaning: true

  domain: 'solved'

  constructor: (operation, engine) ->
    @key ||= @serialize(operation, engine)

  push: ->

  serialize: (operation, engine) ->
    return '@' + @toExpression(operation[1])

  update: (engine, operation, continuation, scope, ascender, ascending) ->

    watchers = engine.queries.watchers[scope._gss_id] ||= []
    if !watchers.length || engine.indexOfTriplet(watchers, operation.parent, continuation, scope) == -1
      watchers.push operation.parent, continuation + @DESCEND, scope

    
    path = continuation + @DESCEND + @key

    old = engine.queries[path]
    if !!old != !!ascending || (old == undefined && old != ascending)
      
      unless old == undefined
        engine.queries.clean(path, continuation, operation.parent, scope)
      unless engine.switching
        switching = engine.switching = true

      engine.queries[path] = ascending
      if switching
        engine.triggerEvent('switch', operation, path)

        if engine.updating
          collections = engine.updating.collections
          engine.updating.collections = {}
          engine.updating.previous = collections



      index = ascending ^ @inverted && 2 || 3
      engine.console.group '%s \t\t\t\t%o\t\t\t%c%s', (index == 2 && 'if' || 'else') + @DESCEND, operation.parent[index], 'font-weight: normal; color: #999', continuation
      if branch = operation.parent[index]
        result = engine.Command(branch).solve(engine, branch, @delimit(path, @DESCEND), scope)

      if switching
        engine.triggerEvent('switch', operation, true)
        engine.switching = undefined
      
      engine.console.groupEnd(path)

  # Capture commands generated by evaluation of arguments
  yield: (result, engine, operation, continuation, scope) ->
    # Condition result bubbled up, pick a branch
    if operation.parent.indexOf(operation) == -1
      if operation[0].key
        continuation = operation[0].key
        scope = engine.identity[operation[0].scope] || scope
      else
        continuation = @delimit(continuation, @DESCEND)
      if continuation?
        @update(engine.document || engine.abstract, operation.parent[1], continuation, scope, undefined, result)
      return true
      
Condition.define 'if', {}
Condition.define 'unless', {
  inverted: true
}
 
module.exports = Condition